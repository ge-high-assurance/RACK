uri "http://arcos.acert/GrammaTech" alias Acert.
 
import "http://arcos.rack/TESTING".
import "http://arcos.rack/DOCUMENT".
import "http://arcos.rack/SOFTWARE".
import "http://arcos.rack/REQUIREMENTS".
import "http://arcos.rack/MODEL".

/* ACERT requirements have confidence parameters on text extraction */
AcertRequirementModel is a type of MODEL.

    textConfidence (note "confidence that this was requirement text")
        describes AcertRequirementModel with values of type double.
	
	ifText (note "conditions under which this requirement holds")
	    describes AcertRequirementModel with values of type string.
    ifTextConfidence (note "confidence that this was the if-portion of the text")
        describes AcertRequirementModel with values of type double.

	thenText (note "specifies what should hold by this requirement if the requirement conditions are met")
		describes AcertRequirementModel with values of type string.    
    thenTextConfidence (note "confidence that this was the then-portion of the text")
        describes AcertRequirementModel with values of type double.
	
	givenText (note "optional part that can be used to set certain conditions apart from other conditions in the ifText part")
		describes AcertRequirementModel with values of type string.
    givenTextConfidence (note "confidence that this was the given-portion of the text")
        describes AcertRequirementModel with values of type double.

AcertTestResult is a type of TEST_RESULT.
    failureReason (note "reason for failure, e.g.: timeout, crash, bad output")
        describes AcertTestResult with values of type FailureReason.

	failureDetails (note "Additional detail describing the failure reason")
	    describes AcertTestResult with values of type string.

AcertTestExecution is a type of TEST_EXECUTION.
    /* Metrics */
    cpuTime (note "seconds of CPU time used when executing the test execution")
        describes AcertTestExecution with values of type double.

    memory (note "maximum bytes of resident memory used by test execution")
        describes AcertTestExecution with values of type double.

FailureReason (note "High-level reasons for ACERT test result failure")
    is a type of THING,
    must be one of {Timeout, Crash, DifferentOutput, Assert}.
Timeout         is a FailureReason with identifier "Timeout".
Crash           is a FailureReason with identifier "Crash".
DifferentOutput is a FailureReason with identifier "DifferentOutput".
Assert          is a FailureReason with identifier "Assert".

MITRE_CWE is a type of ENTITY.
  detectById describes MITRE_CWE with values of type MITRE_CWE_DETECTION.

MITRE_CWE_DETECTION is a type of ENTITY.
  effectiveness describes MITRE_CWE_DETECTION with values of type CWE_EFFECTIVENESS.

CWE_EFFECTIVENESS is a type of THING.
CweDefenseInDepth            is a CWE_EFFECTIVENESS identifier "Defense in Depth".
CweDiscouragedCommonPractice is a CWE_EFFECTIVENESS identifier "Discouraged Common Practice".
CweHigh                      is a CWE_EFFECTIVENESS identifier "High".
CweLimited                   is a CWE_EFFECTIVENESS identifier "Limited".
CweModerate                  is a CWE_EFFECTIVENESS identifier "Moderate".
CweNone                      is a CWE_EFFECTIVENESS identifier "None".
CweOpportunistic             is a CWE_EFFECTIVENESS identifier "Opportunistic".
CweSOAR_Partial              is a CWE_EFFECTIVENESS identifier "SOAR Partial".
