/* Copyright 2021 by Lockheed Martin. */

uri "http://arcos.rack/SACM" alias sacm.
import "http://arcos.rack/PROV-S".


/* Base Classes. */

ELEMENT is a type of ENTITY.
        uuid describes ELEMENT with a single value of type string.

MULTI_LANG_STRING
    is a type of ELEMENT.
        hasValue describes MULTI_LANG_STRING with values of type LANG_STRING.

LANG_STRING
    is a type of ELEMENT.
        lang describes LANG_STRING with values of type string.
        lang of LANG_STRING has at most 1 value.
        contents describes LANG_STRING with values of type string.
        contents of LANG_STRING has at most 1 value.

EXPRESSION_LANG_STRING
    is a type of LANG_STRING.
        expresses describes EXPRESSION_LANG_STRING with values of type EXPRESSION_ELEMENT.

SACM_ELEMENT
    is a type of ELEMENT.
        gid describes SACM_ELEMENT with values of type string.
        gid of SACM_ELEMENT has at most 1 value.
        isCitation describes SACM_ELEMENT with values of type boolean.
        isCitation of SACM_ELEMENT has at most 1 value.
        isAbstract describes SACM_ELEMENT with values of type boolean.
        isAbstract of SACM_ELEMENT has at most 1 value.
        cites describes SACM_ELEMENT with values of type SACM_ELEMENT.
        implements describes SACM_ELEMENT with values of type SACM_ELEMENT.

UTILITY_ELEMENT
    is a type of SACM_ELEMENT.
        hasContent describes UTILITY_ELEMENT with values of type MULTI_LANG_STRING.

TAGGED_VALUE
    is a type of UTILITY_ELEMENT.
        hasKey describes TAGGED_VALUE with values of type MULTI_LANG_STRING.

NOTE
    is a type of UTILITY_ELEMENT.

IMPLEMENTATION_CONSTRAINT
    is a type of UTILITY_ELEMENT.

DESCRIPTION
    is a type of UTILITY_ELEMENT.

MODEL_ELEMENT
    is a type of SACM_ELEMENT.
        hasTaggedValue describes MODEL_ELEMENT with values of type TAGGED_VALUE.
        hasImplementationConstraint describes MODEL_ELEMENT with values of type IMPLEMENTATION_CONSTRAINT.
        hasDescription describes MODEL_ELEMENT with values of type DESCRIPTION.
        hasNote describes MODEL_ELEMENT with values of type NOTE.
        hasName describes MODEL_ELEMENT with values of type LANG_STRING.

ARTIFACT_ELEMENT
    is a type of MODEL_ELEMENT.

/* Assurance Case Package Classes. */

ASSURANCE_CASE_PACKAGE
    is a type of ARTIFACT_ELEMENT.
        hasPackage describes ASSURANCE_CASE_PACKAGE with values of type ASSURANCE_CASE_PACKAGE.
        usesTerminology describes ASSURANCE_CASE_PACKAGE with values of type TERMINOLOGY_PACKAGE.
        hasArgument describes ASSURANCE_CASE_PACKAGE with values of type ARGUMENT_PACKAGE.

/* TERMinology Classes. */

TERMINOLOGY_ELEMENT
    is a type of ARTIFACT_ELEMENT.

TERMINOLOGY_PACKAGE
    is a type of TERMINOLOGY_ELEMENT.
        hasElement describes TERMINOLOGY_PACKAGE with values of type TERMINOLOGY_ELEMENT.

TERMINOLOGY_GROUP
    is a type of TERMINOLOGY_ELEMENT.
        hasElement describes TERMINOLOGY_GROUP with values of type TERMINOLOGY_ELEMENT.

TERMINOLOGY_ASSET
    is a type of TERMINOLOGY_ELEMENT.

CATEGORY
    is a type of TERMINOLOGY_ASSET.

EXPRESSION_ELEMENT
    is a type of TERMINOLOGY_ASSET.
        val describes EXPRESSION_ELEMENT with values of type string.
        val of EXPRESSION_ELEMENT has at most 1 value.
        inCategory describes EXPRESSION_ELEMENT with values of type CATEGORY.

EXPRESSION
    is a type of EXPRESSION_ELEMENT.
        usesElement describes EXPRESSION with values of type EXPRESSION_ELEMENT.

TERM
    is a type of EXPRESSION_ELEMENT.
        externalReference describes TERM with values of type string.
        hasOrigin describes TERM with values of type MODEL_ELEMENT.

/* Argumentation Classes. */

ARGUMENTATION_ELEMENT
    is a type of ARTIFACT_ELEMENT.

ARGUMENT_PACKAGE
    is a type of ARGUMENTATION_ELEMENT.
        hasElement describes ARGUMENT_PACKAGE with values of type ARGUMENTATION_ELEMENT.
        hasElement is a type of wasDerivedFrom.

ARGUMENT_ASSET
    is a type of ARGUMENTATION_ELEMENT.
        hasContent describes ARGUMENT_ASSET with values of type MULTI_LANG_STRING.

ARGUMENT_REASONING
    is a type of ARGUMENT_ASSET.

ARTIFACT_REFERENCE
    is a type of ARGUMENT_ASSET.
        references describes ARTIFACT_REFERENCE with values of type ARTIFACT_ELEMENT.

ASSERTION
    is a type of ARGUMENT_ASSET.
        assertionDeclaration describes ASSERTION with values of type string.

CLAIM
    is a type of ASSERTION.

ASSERTED_RELATIONSHIP
    is a type of ASSERTION.
        isCounter describes ASSERTED_RELATIONSHIP with values of type boolean.
        source describes ASSERTED_RELATIONSHIP with values of type ARGUMENT_ASSET.
        source is a type of wasDerivedFrom.
        target describes ASSERTED_RELATIONSHIP with values of type ARGUMENT_ASSET.
        target is a type of wasDerivedFrom.
        reasoning describes ASSERTED_RELATIONSHIP with values of type ARGUMENT_REASONING.
        reasoning is a type of wasDerivedFrom.

ASSERTED_CONTEXT
    is a type of ASSERTED_RELATIONSHIP.

ASSERTED_EVIDENCE
    is a type of ASSERTED_RELATIONSHIP.

/* Artifact Classes. */

ARTIFACT_PACKAGE
    is a type of ARTIFACT_ELEMENT.

ARTIFACT_ASSET
    is a type of ARTIFACT_ELEMENT.

PROPERTY
    is a type of ARTIFACT_ASSET.

ARTIFACT_ASSET_RELATIONSHIP
    is a type of ARTIFACT_ASSET.
        source describes ARTIFACT_ASSET_RELATIONSHIP with values of type ARTIFACT_ASSET.
        source is a type of wasDerivedFrom.
        target describes ARTIFACT_ASSET_RELATIONSHIP with values of type ARTIFACT_ASSET.
        target is a type of wasDerivedFrom.

ARTIFACT
    is a type of ARTIFACT_ASSET.
        artifactVersion describes ARTIFACT with values of type string.
        artifactDate describes ARTIFACT with values of type string.

ACTIVITY
    is a type of ARTIFACT_ASSET.
        startTime describes ACTIVITY with values of type string.
        endTime describes ACTIVITY with values of type string.

EVENT
    is a type of ARTIFACT_ASSET.
        occurence describes EVENT with values of type string.

PARTICIPANT
    is a type of ARTIFACT_ASSET.

TECHNIQUE
    is a type of ARTIFACT_ASSET.

RESOURCE
    is a type of ARTIFACT_ASSET.
