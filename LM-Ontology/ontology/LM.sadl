/* Copyright 2021 by Lockheed Martin. */

uri "http://arcos.certgate/LM" alias sacm.
import "http://arcos.rack/PROV-S".


/* Base Classes. */

Element is a type of ENTITY.
        uuid describes Element with a single value of type string.

MultiLangString
    is a type of Element.
        hasValue describes MultiLangString with values of type LangString.

LangString
    is a type of Element.
        lang describes LangString with values of type string.
        lang of LangString has at most 1 value.
        contents describes LangString with values of type string.
        contents of LangString has at most 1 value.

ExpressionLangString
    is a type of LangString.
        expresses describes ExpressionLangString with values of type ExpressionElement.

SacmElement
    is a type of Element.
        gid describes SacmElement with values of type string.
        gid of SacmElement has at most 1 value.
        isCitation describes SacmElement with values of type boolean.
        isCitation of SacmElement has at most 1 value.
        isAbstract describes SacmElement with values of type boolean.
        isAbstract of SacmElement has at most 1 value.
        cites describes SacmElement with values of type SacmElement.
        implements describes SacmElement with values of type SacmElement.

UtilityElement
    is a type of SacmElement.
        hasContent describes UtilityElement with values of type MultiLangString.

TaggedValue
    is a type of UtilityElement.
        hasKey describes TaggedValue with values of type MultiLangString.

Note
    is a type of UtilityElement.

ImplementationConstraint
    is a type of UtilityElement.

Description
    is a type of UtilityElement.

ModelElement
    is a type of SacmElement.
        hasTaggedValue describes ModelElement with values of type TaggedValue.
        hasImplementationConstraint describes ModelElement with values of type ImplementationConstraint.
        hasDescription describes ModelElement with values of type Description.
        hasNote describes ModelElement with values of type Note.
        hasName describes ModelElement with values of type LangString.

ArtifactElement
    is a type of ModelElement.
	hasElement describes ArtifactElement with values of type ArtifactElement.
	hasElement is a type of wasDerivedFrom.
	

/* Assurance Case Package Classes. */

AssuranceCasePackage
    is a type of ArtifactElement.
        hasPackage describes AssuranceCasePackage with values of type AssuranceCasePackage.
        usesTerminology describes AssuranceCasePackage with values of type TerminologyPackage.
        hasArgument describes AssuranceCasePackage with values of type ArgumentPackage.

/* TERMinology Classes. */

TerminologyElement
    is a type of ArtifactElement.

TerminologyPackage
    is a type of TerminologyElement.
//        hasElement describes TerminologyPackage with values of type TerminologyElement.

TerminologyGroup
    is a type of TerminologyElement.
//        hasElement describes TerminologyGroup with values of type TerminologyElement.

TerminologyAsset
    is a type of TerminologyElement.

Category
    is a type of TerminologyAsset.

ExpressionElement
    is a type of TerminologyAsset.
        val describes ExpressionElement with values of type string.
        val of ExpressionElement has at most 1 value.
        inCategory describes ExpressionElement with values of type Category.

Expression
    is a type of ExpressionElement.
        usesElement describes Expression with values of type ExpressionElement.

Term
    is a type of ExpressionElement.
        externalReference describes Term with values of type string.
        hasOrigin describes Term with values of type ModelElement.

/* Argumentation Classes. */

ArgumentationElement
    is a type of ArtifactElement.

ArgumentPackage
    is a type of ArgumentationElement.
//        hasElement describes ArgumentPackage with values of type ArgumentationElement.
//        hasElement is a type of wasDerivedFrom.

ArgumentAsset
    is a type of ArgumentationElement.
        argumentContent describes ArgumentAsset with values of type MultiLangString.

ArgumentReasoning
    is a type of ArgumentAsset.

ArtifactReference
    is a type of ArgumentAsset.
        references describes ArtifactReference with values of type ArtifactElement.

Assertion
    is a type of ArgumentAsset.
        assertionDeclaration describes Assertion with values of type string.

Claim
    is a type of Assertion.

AssertedRelationship
    is a type of Assertion.
        isCounter describes AssertedRelationship with values of type boolean.
        source describes AssertedRelationship with values of type ArgumentAsset.
        source is a type of wasDerivedFrom.
        target describes AssertedRelationship with values of type ArgumentAsset.
        target is a type of wasDerivedFrom.
        reasoning describes AssertedRelationship with values of type ArgumentReasoning.
        reasoning is a type of wasDerivedFrom.

AssertedContext
    is a type of AssertedRelationship.

AssertedEvidence
    is a type of AssertedRelationship.

/* Artifact Classes. */

ArtifactPackage
    is a type of ArtifactElement.

ArtifactAsset
    is a type of ArtifactElement.

Property
    is a type of ArtifactAsset.

ArtifactAssetRelationship
    is a type of ArtifactAsset.
        sourceArtifact describes ArtifactAssetRelationship with values of type ArtifactAsset.
        sourceArtifact is a type of wasDerivedFrom.
        targetArtifact describes ArtifactAssetRelationship with values of type ArtifactAsset.
        targetArtifact is a type of wasDerivedFrom.

Artifact
    is a type of ArtifactAsset.
        artifactVersion describes Artifact with values of type string.
        artifactDate describes Artifact with values of type string.

SacmActivity
    is a type of ArtifactAsset.
        startTime describes SacmActivity with values of type string.
        endTime describes SacmActivity with values of type string.

Event
    is a type of ArtifactAsset.
        occurence describes Event with values of type string.

Participant
    is a type of ArtifactAsset.

Technique
    is a type of ArtifactAsset.

Resource
    is a type of ArtifactAsset.
