/* Copyright (c) 2020, General Electric Company, Galois, Inc.
 *
 * All Rights Reserved
 *
 * This material is based upon work supported by the Defense Advanced Research
 * Projects Agency (DARPA) under Contract No. FA8750-20-C-0203.
 *
 * Any opinions, findings and conclusions or recommendations expressed in this
 * material are those of the author(s) and do not necessarily reflect the views
 * of the Defense Advanced Research Projects Agency (DARPA).
 */

/******* URIs and imports ***************/
uri "http://arcos.rack/AGENTS" alias Ag.
import "http://arcos.rack/PROV-S".


/************** edit history *****************
 *
 * dwa - 11 Nov 2020 - reduced cardinality of 'employedBy' to 1, because N seems ill-defined or unnecessary
 * dwa - 11 Nov 2020 - removed 'name' as a string property because it was pushed down into the AGENT base class - see edit in PROV-S.sadl
 * dwa - 11 Nov 2020 - added 'version' as a string property for TOOL agents
 * am - 31 Aug 2021 - altered employedBy so that range and cardinality info is separated out; and use "1" instead of "one"
 */

ORGANIZATION
  (note "An organization is a social or legal institution such as a company, society, etc.")
  is a type of AGENT.

PERSON (note "Person agents are people - alias for User")
  is a type of AGENT.
  emailAddress (note "Email address of person")
    describes PERSON with values of type string.
  employedBy (note "Relates people to the company they were acting on behalf of.")
    describes PERSON with values of type AGENT.
  employedBy describes PERSON with at most 1 value of type AGENT.
  employedBy is a type of actedOnBehalfOf.
  role (note "The role a person played in the performance of an activity (tester, witness, etc.)") 
  	describes PERSON with values of type string.

TOOL
  (note "An aid (software, hardware, etc) that automates the performance of an activity.")
  is a type of AGENT.
  toolInstallationConfiguration describes TOOL with values of type ENTITY.
  toolInstallationConfiguration describes TOOL with at most 1 value.
  toolVersion (note "The release version of the tool") describes TOOL with values of type string.
  toolVersion describes TOOL with at most 1 value.
  wasQualifiedBy describes TOOL with values of type ENTITY. //AM 9/27/2021: added, do not expect more than 1 as cardinality but no need to add in about it
