/* Copyright (c) 2020, General Electric Company, Galois, Inc.
 *
 * All Rights Reserved
 *
 * This material is based upon work supported by the Defense Advanced Research
 * Projects Agency (DARPA) under Contract No. FA8750-20-C-0203.
 *
 * Any opinions, findings and conclusions or recommendations expressed in this
 * material are those of the author(s) and do not necessarily reflect the views
 * of the Defense Advanced Research Projects Agency (DARPA).
 */


/************** edit history *****************
  *
  * dwa - 11 Nov 2020 - changed ANALYSIS_REPORT to a kind of REPORT vs. ENTITY, since REPORT is defined already
  * dwa - 11 Nov 2020 - minor updates to note fields
  * dwa - 11 Nov 2020 - reduced cardinality of result in ANALYSIS_REPORT to 1, since N seems ill-defined
  * dwa - 11 Nov 2020 - reduced cardinality of ANALYSIS_REPORT in ANALYSIS_ANNOTATION to 1, since an annotation will naturally come from a single report
  * dwa - 13 Nov 2020 - added import for DOCUMENT to allow ANALYSIS_REPORT to be a kind of REPORT
  */

uri "http://arcos.rack/ANALYSIS" alias ^An.
import "http://arcos.rack/PROV-S".
//import "http://arcos.rack/PROCESS".

ANALYSIS
	(note "ACTIVITY of running some analysis, and as a result creating some ANALYSIS_OUTPUT")
	is a type of ACTIVITY.

    runBy (note "Agent responsible for running the analysis.") describes ANALYSIS with values of type AGENT.
    runBy is a type of wasAssociatedWith.

    analyzedWith (note "Tool responsible for producing the output.") describes ANALYSIS with values of type AGENT. //AM 9/27/2021 was ENTITY earlier
    analyzedWith is a type of wasAssociatedWith. //AM 9/27/2021 altered this as the previous line was altered
    
    analysisInput (note "Entities targeted by the analysis.") describes ANALYSIS with values of type ENTITY.
    analysisInput is a type of used.

    analysisConfiguration (note "Parameters/options etc. for the tool used.") describes ANALYSIS with values of type ENTITY.
    analysisConfiguration is a type of used.


ANALYSIS_OUTPUT
    (note "Output of an ANALYSIS")
    is a type of ENTITY.

    analyzes (note "ENTITY(s) under analysis") describes ANALYSIS_OUTPUT with values of type ENTITY.
    analyzes is a type of wasImpactedBy.
    
    wasGeneratedBy of ANALYSIS_OUTPUT only has values of type ANALYSIS.
