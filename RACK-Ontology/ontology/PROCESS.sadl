/* Copyright (c) 2020, General Electric Company, Galois, Inc.
 *
 * All Rights Reserved
 *
 * This material is based upon work supported by the Defense Advanced Research
 * Projects Agency (DARPA) under Contract No. FA8750-20-C-0203.
 *
 * Any opinions, findings and conclusions or recommendations expressed in this
 * material are those of the author(s) and do not necessarily reflect the views
 * of the Defense Advanced Research Projects Agency (DARPA).
 */

uri "http://arcos.rack/PROCESS" alias Pr.
import "http://arcos.rack/PROV-S".

OBJECTIVE 
	(note "An OBJECTIVE identifies tasks from a process for which evidence must be provided to show that the task has been completed. ") 
	is a type of ENTITY.

	satisfiedBy (note "An ACTIVITY that demonstrates that the OBJECTIVE has been satisfied.") describes OBJECTIVE with values of type ACTIVITY.

PROPERTY 
	(note "A general property that holds on the basis of some ANALYSIS")
	is a type of ENTITY. 

	propertyType (note "The semantic tag for this property instance.")
	    describes PROPERTY with a single value of type PROPERTY_TYPE.

	partiallySupports (note "One or more PROPERTY may be needed to address an OBJECTIVE.") describes PROPERTY with values of type OBJECTIVE.

    scopeOf (note "ENTITY(s) over which the PROPERTY holds") describes PROPERTY with values of type ENTITY.
    scopeOf is a type of wasImpactedBy.

    mitigates (note "ENTITY(s) that is being mitigated by this PROPERTY") describes PROPERTY with values of type ENTITY.
    mitigates is a type of wasImpactedBy.

PROPERTY_TYPE
    (note "The target-independent identifier of the semantics of a PROPERTY.") 
    is a type of THING.

RequirementSatisfaction
    is a PROPERTY_TYPE
    identifier "RequirementSatisfaction"
    title "Requirement Satisfaction".

StatementCoverage
    is a PROPERTY_TYPE
    identifier "StatementCoverage"
    title "Statement Coverage".

OracleMonitoredCoverage
    is a PROPERTY_TYPE
    identifier "OracleMonitoredCoverage"
    title "Oracle Monitored Coverage".

InputSpaceCoverage
    is a PROPERTY_TYPE
    identifier "InputSpaceCoverage"
    title "InputSpaceCoverage".

Cwe is a PROPERTY_TYPE
    identifier "Cwe"
    title "CWE Property".

Issue
    is a PROPERTY_TYPE
    identifier "Issue"
    title "Issue".