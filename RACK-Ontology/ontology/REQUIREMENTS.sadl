uri "http://arcos.rack/REQUIREMENTS" alias Rq.
import "http://arcos.rack/PROV-S".

REQUIREMENT
	(note "captures (both high- and low-level) properties of a process or artifact that are to be assessed")
	is a type of ENTITY.
	text describes REQUIREMENT with values of type string.

	governs (note "trace to ENTITYs that are the subject of the requirement") describes REQUIREMENT with values of type ENTITY.
	governs is a type of wasDerivedFrom.

	satisfies (note "trace to a parent ENTITY (e.g. REQUIREMENT) that this REQUIREMENT is derived from") describes REQUIREMENT with values of type ENTITY.
	satisfies is a type of wasDerivedFrom.

	mitigates (note "trace to an ENTITY (e.g. HAZARD) that is being mitigated by this REQUIREMENT") describes REQUIREMENT with values of type ENTITY.
	mitigates is a type of wasDerivedFrom.

	createdBy (note "trace to an ACTIVITY (e.g. REQUIREMENT_DEVELOPMENT) that created this REQUIREMENT") describes REQUIREMENT with values of type ACTIVITY.
	createdBy is a type of wasGeneratedBy.


DATA_DICTIONARY_TERM
	(note "defines some conceptual data that may be collected somewhere and used somewhere else")
	is a type of ENTITY.
	text describes DATA_DICTIONARY_TERM with values of type string.

	providedBy (note "TODO") describes DATA_DICTIONARY_TERM with values of type ENTITY.
	providedBy is a type of wasDerivedFrom.

	consumedBy (note "ENTITYs that may consume this information") describes DATA_DICTIONARY_TERM with values of type ENTITY.
	consumedBy is a type of wasDerivedFrom.

	createdBy (note "TODO") describes DATA_DICTIONARY_TERM with values of type ACTIVITY.
	createdBy is a type of wasGeneratedBy.


REQUIREMENT_DEVELOPMENT
	(note "an ACTIVITY that produces REQUIREMENTs")
	is a type of ACTIVITY.
	author (note "AGENT responsible for producing a REQUIREMENT") describes REQUIREMENT_DEVELOPMENT with values of type AGENT.
	author is a type of wasAssociatedWith.

	referenced (note "source materials (requirement and design docs) used during development") describes REQUIREMENT_DEVELOPMENT with values of type ENTITY.
	referenced is a type of used.

	governedBy (note "processes documents used") describes REQUIREMENT_DEVELOPMENT with values of type ENTITY.
	governedBy is a type of used.
