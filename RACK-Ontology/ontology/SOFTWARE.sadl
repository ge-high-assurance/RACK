uri "http://arcos.rack/SOFTWARE" alias sw.
import "http://arcos.rack/PROV-S".

FILE
    (note "A file in the system")
	is a type of ENTITY.
	filename (note "A logical file path relative to the container of the file.") describes FILE with values of type string.

	fileFormat (note "The byte-level encoding of a file") describes FILE with values of type FORMAT.

    satisfies (note "ENTITY(s) (e.g. REQUIREMENT) that this code file participates in satisfying") describes FILE with values of type ENTITY.
    satisfies is a type of wasDerivedFrom.

    createBy (note "FILE_CREATION ACTIVITY that created this file") describes FILE with values of type FILE_CREATION.
    createBy is a type of wasGeneratedBy.

FORMAT
    (note "The data format of a FILE")
    is a type of THING.

FILE_CREATION
   (note "An ACTIVITY that produces a FILE")
   is a type of ACTIVITY.

CODE_DEVELOPMENT
	(note "An ACTIVITY that produces CODE_FILEs")
	is a type of FILE_CREATION.

    author (note "AGENT(s) who participated in this ACTIVITY") describes CODE_DEVELOPMENT with values of type AGENT.
    author is a type of wasAssociatedWith.

    referenced (note "ENTITY(s) (e.g. REQUIREMENT) that were referenced during this ACTIVITY") describes CODE_DEVELOPMENT with values of type ENTITY.
    referenced is a type of used.

    governedBy (note "ENTITY(s) (e.g. REQUIREMENT) that gave rise to this ACTIVITY") describes CODE_DEVELOPMENT with values of type ENTITY.
    governedBy is a type of used.

BUILD
	(note "Describes a set of ACTIVITYs (e.g. CODE_GEN, COMPILE, ...) that together form some logical building unit")
	is a type of FILE_CREATION.
    step (note "A BUILD may be comprised of any number of sub-ACTIVITY(s)") describes BUILD with values of type ACTIVITY.
    step is a type of wasInformedBy.

CODE_GEN
	(note "ACTIVITY of running a CODE_GENERATOR to create CODE_FILEs")
	is a type of FILE_CREATION.
    performedBy (note "CODE_GENERATOR(s) generating the code") describes CODE_GEN with values of type AGENT.
    performedBy is a type of wasAssociatedWith.

COMPILE
	(note "ACTIVITY of running a a COMPILER to produce EXECUTABLEs or OBJECT_FILEs")
	is a type of FILE_CREATION.
    performedBy (note "AGENT(s) (e.g. COMPILER) performing the compilation") describes COMPILE with values of type AGENT.
    performedBy is a type of wasAssociatedWith.
    
    compiledBy (note "The tool that performed the compilation") describes COMPILE with values of type FILE.
    compiledBy is a type of used.
    
    compileInput (note "Source files that contributed to the compilation") describes COMPILE with values of type FILE.
    compileInput is a type of used.

PACKAGE
	(note "ACTIVITY of running a PACKAGER to produce a PACKAGE_FILE")
	is a type of FILE_CREATION.
    performedBy (note "AGENT(s) (e.g. PACKAGER) performing the packaging") describes PACKAGE with values of type AGENT.
    performedBy is a type of wasAssociatedWith.

	packagedBy (note "The tool used to generate the package output") describes PACKAGE with values of type FILE.
	packagedBy is a type of used.

	packageInput (note "Source files used when generating the package output") describes PACKAGE with values of type FILE.
	packageInput is a type of used.

COMPONENT_TYPE
    (note "An open/extensible set of types of software components")
    is a type of THING.

// A few common instances:
SOURCE_FUNCTION
    (note "A function or procedure declared or defined in source code.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "SOURCE_FUNCTION".
BINARY_FUNCTION
    (note "A function in a binary, as defined by the appropriate ABI.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "BINARY_FUNCTION".
SOURCE_GLOBAL_VARIABLE
    (note "A global variable declared or defined in source code.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "SOURCE_GLOBAL_VARIABLE".
BINARY_GLOBAL_VARIABLE
    (note "A global variable (generally in the .data or .bss sections).")
    is a COMPONENT_TYPE
    has uniqueIdentifier "BINARY_GLOBAL_VARIABLE".
BINARY_BASIC_BLOCK
    (note "A basic block at the binary level.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "BINARY_BASIC_BLOCK".
CLASS
    (note "A class in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS".
CLASS_METHOD
    (note "A method attached to a class in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS_METHOD".
CLASS_MEMBER_VARIABLE
    (note "A variable attached to the instances of a class in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS_MEMBER_VARIABLE".
CLASS_CONSTRUCTOR
    (note "A constructor in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS_CONSTRUCTOR".
MODULE
    (note "A collection of related code, usually grouped in a lexical scope.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "MODULE".
NAMESPACE
    (note "A collection of related code, usually grouped in a lexical scope.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "NAMESPACE".

COMPONENT is a type of ENTITY.
    name describes COMPONENT with values of type string.
    componentType describes COMPONENT with a single value of type COMPONENT_TYPE.
    valueType (note "The type of this value, if applicable (e.g. for functions or variables).") describes COMPONENT with values of type string.
    instantiates (note "What logical component (system) does this physical component (code) instantiate or implement?") describes COMPONENT with values of type ENTITY.
    definedIn (note "Defining entity of this code structure. This could be a CODE_FILE, LIBRARY, etc.") describes COMPONENT with values of type ENTITY.
    mentions (note "A component referenced by this one, e.g., a callee or variable being used.") describes COMPONENT with values of type ENTITY.
    subcomponentOf (note "A structural sub-component, e.g., a function might be a subcomponent of module.") describes COMPONENT with values of type ENTITY.
    requirements (note "Low-level requirements that apply to this component.") describes COMPONENT with values of type ENTITY.
    annotations (note "Analysis annotations that apply to this component.") describes COMPONENT with values of type ENTITY.
    controlFlowsToUnconditionally (note "Control flows from this component to the target unconditionally.") describes COMPONENT with values of type COMPONENT.
    controlFlowsToConditionally (note "Control flows from this component to the target conditionally.") describes COMPONENT with values of type COMPONENT.
