uri "http://arcos.rack/SOFTWARE" alias sw.
import "http://arcos.rack/PROV-S".

PACKAGE_FILE is a type of ENTITY.
PACKAGE_FILE (note "a package file, e.g. Reg Hat's .rpm, Debian's .deb, etc.").

CODE_FILE is a type of ENTITY.
CODE_FILE (note "file that contains code that may be compiled and/or executed").
    satisfies (note "ENTITY(s) (e.g. REQUIREMENT) that this code file participates in satisfying") describes CODE_FILE with values of type ENTITY.
    satisfies is a type of wasDerivedFrom.

    sw:language (note "programming language(s) of this file") describes CODE_FILE with values of type LANGUAGE.
    sw:language is a type of wasDerivedFrom.

    createBy (note "ACTIVITY(s) that created this file") describes CODE_FILE with values of type ACTIVITY.
    createBy is a type of wasGeneratedBy.

LANGUAGE is a type of ENTITY.
LANGUAGE (note "describes a programming language, possibly identifying some version of its specification").
    ^version (note "version of the language, where applicable") describes LANGUAGE with values of type string.




CONFIG_FILE is a type of ENTITY.
CONFIG_FILE (note "anything that can be considered configuration").
    createBy (note "ACTIVITY(s) that created this file") describes CONFIG_FILE with values of type ACTIVITY.
    createBy is a type of wasGeneratedBy.

OBJECT_FILE is a type of ENTITY.
OBJECT_FILE (note "compiled object code, not ready for execution").
    createBy (note "ACTIVITY(s) that created this file") describes OBJECT_FILE with values of type ACTIVITY.
    createBy is a type of wasGeneratedBy.

LIBRARY is a type of ENTITY.
LIBRARY (note "shared library").
    createBy (note "ACTIVITY(s) that created this file") describes LIBRARY with values of type ACTIVITY.
    createBy is a type of wasGeneratedBy.

EXECUTABLE is a type of ENTITY.
EXECUTABLE (note "compiled executable, ready for execution").
    createBy (note "ACTIVITY(s) that created this file") describes EXECUTABLE with values of type ACTIVITY.
    createBy is a type of wasGeneratedBy.

CODE_GENERATOR is a type of AGENT.
CODE_GENERATOR (note "a tool that generates CODE_FILEs as part of a CODE_GEN ACTIVITY").

COMPILER is a type of AGENT.
COMPILER (note "a tool that compiles CODE_FILEs into ENTITYs such as OBJECT_FILE or EXECUTABLE").

PACKAGER is a type of AGENT.
PACKAGER (note "a tool that packages some software together as part of a PACKAGE ACTIVITY").

CODE_DEVELOPMENT is a type of ACTIVITY.
CODE_DEVELOPMENT (note "an ACTIVITY that produces CODE_FILEs").
    author (note "AGENT(s) who participated in this ACTIVITY") describes CODE_DEVELOPMENT with values of type AGENT.
    author is a type of wasAssociatedWith.

    referenced (note "ENTITY(s) (e.g. REQUIREMENT) that were referenced during this ACTIVITY") describes CODE_DEVELOPMENT with values of type ENTITY.
    referenced is a type of used.

    governedBy (note "ENTITY(s) (e.g. REQUIREMENT) that gave rise to this ACTIVITY") describes CODE_DEVELOPMENT with values of type ENTITY.
    governedBy is a type of used.


BUILD is a type of ACTIVITY.
BUILD (note "describes a set of ACTIVITYs (e.g. CODE_GEN, COMPILE, ...) that together form some logical building unit").
    step (note "a BUILD may be comprised of any number of sub-ACTIVITY(s)") describes BUILD with values of type ACTIVITY.
    step is a type of wasInformedBy.

CODE_GEN is a type of ACTIVITY.
CODE_GEN (note "ACTIVITY of running a CODE_GENERATOR to create CODE_FILEs").
    performedBy (note "AGENT(s) (e.g. CODE_GENERATOR) generating the code") describes CODE_GEN with values of type AGENT.
    performedBy is a type of wasAssociatedWith.

COMPILE is a type of ACTIVITY.
COMPILE (note "ACTIVITY of running a a COMPILER to produce EXECUTABLEs or OBJECT_FILEs").
    performedBy (note "AGENT(s) (e.g. COMPILER) performing the compilation") describes COMPILE with values of type AGENT.
    performedBy is a type of wasAssociatedWith.

PACKAGE is a type of ACTIVITY.
PACKAGE (note "ACTIVITY of running a PACKAGER to produce a PACKAGE_FILE").
    performedBy (note "AGENT(s) (e.g. PACKAGER) performing the packaging") describes PACKAGE with values of type AGENT.
    performedBy is a type of wasAssociatedWith.

COMPONENT_TYPE
    (note "An open/extensible set of types of software components")
    is a class.
    uniqueIdentifier describes COMPONENT_TYPE with a single value of type string.

// A few common instances:
SOURCE_FUNCTION
    (note "A function or procedure declared or defined in source code.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "SOURCE_FUNCTION".
BINARY_FUNCTION
    (note "A function in a binary, as defined by the appropriate ABI.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "BINARY_FUNCTION".
SOURCE_GLOBAL_VARIABLE
    (note "A global variable declared or defined in source code.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "SOURCE_GLOBAL_VARIABLE".
BINARY_GLOBAL_VARIABLE
    (note "A global variable (generally in the .data or .bss sections).")
    is a COMPONENT_TYPE
    has uniqueIdentifier "BINARY_GLOBAL_VARIABLE".
BINARY_BASIC_BLOCK
    (note "A basic block at the binary level.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "BINARY_BASIC_BLOCK".
CLASS
    (note "A class in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS".
CLASS_METHOD
    (note "A method attached to a class in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS_METHOD".
CLASS_MEMBER_VARIABLE
    (note "A variable attached to the instances of a class in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS_MEMBER_VARIABLE".
CLASS_CONSTRUCTOR
    (note "A constructor in an object-oriented language.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "CLASS_CONSTRUCTOR".
MODULE
    (note "A collection of related code, usually grouped in a lexical scope.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "MODULE".
NAMESPACE
    (note "A collection of related code, usually grouped in a lexical scope.")
    is a COMPONENT_TYPE
    has uniqueIdentifier "NAMESPACE".

COMPONENT is a type of ENTITY.
    name describes COMPONENT with values of type string.
    componentType describes COMPONENT with a single value of type COMPONENT_TYPE.
    valueType (note "The type of this value, if applicable (e.g. for functions or variables).") describes COMPONENT with values of type string.
    instantiates (note "What logical component (system) does this physical component (code) instantiate or implement?") describes COMPONENT with values of type ENTITY.
    definedIn (note "Defining entity of this code structure. This could be a CODE_FILE, LIBRARY, etc.") describes COMPONENT with values of type ENTITY.
    mentions (note "A component referenced by this one, e.g., a callee or variable being used.") describes COMPONENT with values of type ENTITY.
    subcomponentOf (note "A structural sub-component, e.g., a function might be a subcomponent of module.") describes COMPONENT with values of type ENTITY.
    requirements (note "Low-level requirements that apply to this component.") describes COMPONENT with values of type ENTITY.
    annotations (note "Analysis annotations that apply to this component.") describes COMPONENT with values of type ENTITY.
