/* Copyright (c) 2020-2021, General Electric Company, Galois, Inc.
 *
 * All Rights Reserved
 *
 * This material is based upon work supported by the Defense Advanced Research
 * Projects Agency (DARPA) under Contract No. FA8750-20-C-0203.
 *
 * Any opinions, findings and conclusions or recommendations expressed in this
 * material are those of the author(s) and do not necessarily reflect the views
 * of the Defense Advanced Research Projects Agency (DARPA).
 */

 /************** edit history *****************
  *
  * dwa - 11 Nov 2020 - removed incorrect usages of relationships
  *
  *********************************************/

uri "http://arcos.rack/TESTING" alias tst.
import "http://arcos.rack/PROV-S".

TEST
	(note "Description of a single test")
	is a type of ENTITY.

	verifies (note "ENTITY(s) (e.g. REQUIREMENT) that this test verifies") describes TEST with values of type ENTITY.
	verifies is a type of wasImpactedBy.
	
	wasGeneratedBy of TEST only has values of type TEST_DEVELOPMENT. 

TEST_STATUS (note "The high-level outcome of a TEST_RESULT")
    is a type of THING,
    must be one of {Passed, Failed, Indeterminate}.
Passed        is a TEST_STATUS with identifier "Passed".
Failed        is a TEST_STATUS with identifier "Failed".
Indeterminate is a TEST_STATUS with identifier "Indeterminate".

TEST_RESULT
	(note "Result corresponding to one execution of one TEST")
	is a type of ENTITY.

	result (note "Pass/Failed/Indeterminate status resulting from a TEST_EXECUTION") describes TEST_RESULT with a single value of type TEST_STATUS.

	confirms (note "Parent test(s) of this test result") describes TEST_RESULT with values of type TEST.
	confirms is a type of wasImpactedBy.
	
	wasGeneratedBy of TEST_RESULT only has values of type TEST_EXECUTION.


TEST_PROCEDURE (note "A TEST_PROCEDURE is the TEST_STEPs that perform TESTs. ") is a type of COLLECTION.
	content of TEST_PROCEDURE only has values of type TEST_STEP.
	
	TEST_STEP (note "A TEST_STEP is a collection of TESTs that are performed in a single execution")is a type of COLLECTION.
		content of TEST_STEP only has values of type TEST.
		nextStep (note "The next TEST_STEP in a sequence" ) describes TEST_STEP with values of type TEST_STEP.
		nextStep of TEST_STEP has at most 1 value.

TEST_LOG (note "A TEST_LOG is the TEST_RESULTs from executing a TEST_PROCEDURE") is a type of COLLECTION.
	content of TEST_LOG only has values of type TEST_RECORD.

	
	TEST_RECORD (note "a TEST_RECORD is a collection of TEST_RESULTs associated with a TEST_STEP") is a type of COLLECTION.
		content of TEST_RECORD only has values of type TEST_RESULT.
		nextRecord (note "The next TEST_RECORD that is the next in a sequence " ) describes TEST_RECORD with values of type TEST_RECORD.
		nextRecord of TEST_RECORD has at most 1 value.
		logs (note "TEST_STEP for which the TEST_RECORD is captured" ) describes TEST_RECORD with a single value of type TEST_STEP.
		logs is a type of wasImpactedBy.


TEST_DEVELOPMENT
	(note "ACTIVITY of creating a TEST to participate in the evaluation of a requirement")
	is a type of ACTIVITY.
	
TEST_EXECUTION
	(note "ACTIVITY of performing a TEST and record its corresponding TEST_RESULT")
	is a type of ACTIVITY.

	testProcedure (note "TEST_PROCEDURE that was executed.") describes TEST_EXECUTION with values of type TEST_PROCEDURE.
	testProcedure is a type of used.
