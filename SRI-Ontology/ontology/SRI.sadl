
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// DesCert Ontology
// Copyright 2021-2022. Honeywell International and SRI International

// namespace and imports
uri "http://arcos.descert/SRI" alias DesCert.
import "http://arcos.rack/PROV-S".
import "http://arcos.rack/DOCUMENT".
import "http://arcos.rack/SYSTEM".
import "http://arcos.rack/SOFTWARE".
import "http://arcos.rack/REQUIREMENTS".
// import "http://arcos.rack/REVIEW".
import "http://arcos.rack/TESTING".
import "http://arcos.rack/HAZARD".
import "http://arcos.rack/ANALYSIS".
import "http://arcos.rack/PROCESS".
import "http://arcos.rack/AGENTS".
import "http://arcos.rack/MODEL".
import "http://arcos.AH-64D/Boeing".
   
    DesCert_Tool is a type of TOOL. 
      // note: toolUsageSummary is in addition to the description which just describes the tool in 1-2 sentences
      toolUsageSummary (note "concise description of tool usage in assurance, function, inputs, outputs") describes DesCert_Tool with values of type string.
      toolUsageSummary describes DesCert_Tool with at most 1 value.
       userGuide describes DesCert_Tool with values of type DOCUMENT. 
       toolQualificationData describes DesCert_Tool with values of type ToolQualificationData.
       toolQualificationData describes DesCert_Tool with at most 1 value.
       // DB 1/6/22: constrain the range of wasQualifiedBy 
       wasQualifiedBy of DesCert_Tool only has values of type ORGANIZATION.

    // DB 2/7/22: renamed ConfigurationString to ToolConfigurationInstance
    ToolConfigurationInstance (note "Specific tool command, options, and configuration settings for an activity instance") 
          is a type of ENTITY.

    // DB 2/25/22: added GenerationInformation to hold common properties about different types of generation
    GenerationInformation (note "Information about tool usage in a generation activity.") is a type of ENTITY.
       generatedBy (note "Agent responsible for running the generation using a tool.") describes GenerationInformation with values of type AGENT.
       generatedBy is a type of wasAttributedTo.
       generatedWith (note "Tool responsible for doing the generation to produce outputs.") describes GenerationInformation with values of type TOOL.
       generatedWith is a type of wasAttributedTo.
       generationConfiguration (note "command, parameters, options etc. for the tool used.") describes GenerationInformation with values of type ToolConfigurationInstance.
       generationConfiguration is a type of wasImpactedBy.     
    
    ToolQualificationData
        (note "tool qualification evidence including tool qualification plan, tool operational requirements, and tool verification procedures")
      is a type of ENTITY.
       // relevant properties inherited from ENTITY:
       //   identifier
       //   generatedAtTime
       // relationships inherited from ENTITY that are subtyped:

      toolQualificationSummary (note "summary of tool qualification argument and roadmap of artifacts") describes ToolQualificationData with values of type string.
       toolQualificationSummary describes ToolQualificationData with at most 1 value.
       toolQualificationArtifacts (note "consist of multiple documents") describes ToolQualificationData with values of type DOCUMENT.
       // note: each DOCUMENT in toolQualificationArtifacts is a collection of FILES (or other entities) that have same version or related provenance

     ////////////////////////////////////////////////
     // properties and analysis results 
      
      SpecificProperty is a type of PROPERTY.
         propertyBasis (note "entities impacting the property definition - e.g. SystemconOps") describes SpecificProperty with values of type ENTITY.
         propertyBasis is a type of wasImpactedBy.
         // NOTE: a SpecificProperty is modeled by entities such as SallyPropertyModel; that's where the property text is contained
         
      // DB 1/6/22: added PropertyResult and PropertyProofStatus, these are fundamental to using properties, analoguous to TEST_RESULT
      //   (I had earlier (Nov 21) had added them to core ontology in PROCESS.sadl, but that change was not accpted, so adding in the overlay)
      // DB 2/7/22: made PropertyResult a subclass of ANALYSIS_OUTPUT so that it jives better with core ontology
      PropertyResult (note "Result corresponding to one analysis attempt to prove one PROPERTY") is a type of ANALYSIS_OUTPUT.
         propertyResult (note "Valid/Invalid/Indeterminate status of this PropertyResult") describes PropertyResult with a single value of type PropertyProofStatus.
         // The property being proven by this property result; the property has all relevant information about scope and basis
         analyzes of PropertyResult only has values of type PROPERTY. // DB 2/7/22: may need to put range constraint of a single value
         resultDiagnostics (note "counter examples, other diagnostics") describes PropertyResult with values of type ENTITY.
         wasGeneratedBy of PropertyResult only has values of type ANALYSIS.

      PropertyProofStatus (note "Outcome of the attempt to prove a property") is a type of THING,
         must be one of {Valid, Invalid, Unknown}.
         Valid (note "property is valid (proven to hold)") is a PropertyProofStatus with identifier "Valid".
         Invalid (note "property is invalid (proven to not hold)") is a PropertyProofStatus with identifier "Invalid".
         Unknown (note "property cannot be proven to hold or to not hold") is a PropertyProofStatus with identifier "Unknown".
         
     ////////////////////////////////////////////////
     // System architecture and analysis
   
     RadlNotation (note "RADL notation for architecture specification") is a type of ENTITY.
        userGuide describes RadlNotation with values of type DOCUMENT.
        semantics describes RadlNotation with values of type DOCUMENT.
        
     RadlArchitectureModel (note "The Radl specification (model) of architecture") is a type of MODEL.
        radlArchitectureNotation describes RadlArchitectureModel with a single value of type RadlNotation. 
        radlArchitectureNotation is a type of wasImpactedBy.
        radlSpecification (note "the Radl specification text") describes RadlArchitectureModel with a single value of type string.
        models of RadlArchitectureModel only has values of type SystemArchitecture.

      RadlGenericProperty is a type of PROPERTY.
        scopeOf of RadlGenericProperty only has values of type RadlArchitectureModel.
      
      RadlerArchitectureAnalysis (note "Analysis done by Radler tool on Radl architecture specificaton (SystemArchitecture)") is a type of ANALYSIS.
        // Inherited properties from ANALYSIS
        //   runBy - refers to user
        //   analyzedwith refers to Radler tool
        analysisConfiguration of RadlerArchitectureAnalysis only has values of type ToolConfigurationInstance.
        analysisInput of RadlerArchitectureAnalysis only has values of type {RadlArchitectureModel or RadlGenericProperty}. 
      
      ///////////////////////////////////////////////////
      // System, Software requirements, design, code   
      
    // DB 2/27/22: Added new class RequirementSet with common properties for requirement sets
    RequirementSet (note "a set of requirements that together defines the required behavior of a SYSTEM (or part thereof)")
      is a type of COLLECTION.
      content of RequirementSet only has values of type REQUIREMENT.
      // note: 'speficies' is not a subtype of 'wasImpactedBy, which has the reverse meaning. RequiremenSet occurs before the SYSTEM is constructed.
      specifies (note "the system or component this RequirementSet governs/specifies") describes RequirementSet with values of type ENTITY.

    // DB 1/6/22: we need to keep it separate from PIDS_Req in Beoing.sadl, since the level/abstraction of information in a
    //     typical system requirmeent may be different than PIDS_Req 
    SystemRequirement is a type of REQUIREMENT.
        wasGeneratedBy of SystemRequirement only has values of type SystemRequirementsDefinition.
    
    SystemRequirementSet is a type of RequirementSet.
       content of SystemRequirementSet only has values of type SystemRequirement. 
       wasGeneratedBy of SystemRequirementSet only has values of type SystemRequirementsDefinition.
       
    // DB 1/6/22: we need to keep it separate from SRS_Req in Boeing.sadl, since the level of information in a
    //     typical SW HLR (e.g., CLEAR) is much more detailed than SRS_Req. The satisfies relation
    SoftwareHighLevelRequirement is a type of REQUIREMENT.
      wasGeneratedBy of SoftwareHighLevelRequirement only has values of type SoftwareHighLevelRequirementsDefinition.

    SoftwareHighLevelRequirementSet is a type of RequirementSet.
       content of SoftwareHighLevelRequirementSet only has values of type SoftwareHighLevelRequirement.
       wasGeneratedBy of SoftwareHighLevelRequirementSet only has values of type SoftwareHighLevelRequirementsDefinition.
       specifies of SoftwareHighLevelRequirementSet only has values of type SWCOMPONENT.

    // DB 1/6/22: we need to keep it separate from SubDD_Req in Beoing.sadl
    SoftwareLowLevelRequirement is a type of REQUIREMENT.
        wasGeneratedBy of SoftwareLowLevelRequirement only has values of type SoftwareDesign.
    
    SoftwareLowLevelRequirementSet is a type of RequirementSet.
       content of SoftwareLowLevelRequirementSet only has values of type SoftwareLowLevelRequirement. // DB 2/1/22: was SubDD_Req
       wasGeneratedBy of SoftwareLowLevelRequirementSet only has values of type SoftwareDesign. 
       specifies of SoftwareLowLevelRequirementSet only has values of type SWCOMPONENT.
       
      ClearNotation (note "CLEAR notation for writing requirements") is a type of ENTITY.
        userGuide describes ClearNotation with values of type DOCUMENT.
        semantics describes ClearNotation with values of type DOCUMENT.
          
      DesCertRequirementModel (note "The model of requirement in DesCert in CLEAR notation") is a type of MODEL.
        requirementNotation describes DesCertRequirementModel with a single value of type ClearNotation. 
        requirementNotation is a type of wasImpactedBy.
        requirementText describes DesCertRequirementModel with a single value of type string. 
        // DB 2/9/22: use SoftwareHighLevelRequirement and SoftwareLowLevelRequirement rather than SRS_Req or SubDD_Req
        models of DesCertRequirementModel only has values of type {SoftwareHighLevelRequirement or SoftwareLowLevelRequirement}.

      SourceCode is a type of FILE.
        wasGeneratedBy of SourceCode only has values of type SoftwareCoding.
        
      ObjectFile is a type of FILE.
        wasGeneratedBy of ObjectFile only has values of type SoftwareIntegration. 
      
      // DB 2/7/2: inherit from BUILD rather than COMPILE
      SoftwareIntegration is a type of BUILD. 
        wasInformedBy of SoftwareIntegration only has values of type SoftwareCoding.       
        compileWithOptimizations describes  SoftwareIntegration with values of type boolean.    
        linkerPath describes  SoftwareIntegration with values of type string.      
     
      DataDictionary is a type of COLLECTION.
        //  allow a whole dictionary of terms to be traced as a unit rather than each term individually
        wasGeneratedBy of DataDictionary only has values of type SoftwareHighLevelRequirementsDefinition.
        content of DataDictionary only has values of type DATA_DICTIONARY_TERM.

      ExecutableObject is a type of FILE.
      
      ///////////////////////////////////////////
      // Analysis done by Sally tool
      
      SallyTransitionSystemModel is a type of ENTITY.
        // inherited properties
        //   wasImpactedBy refers to dictionary entities used
        wasDerivedFrom of SallyTransitionSystemModel only has values of type {SoftwareHighLevelRequirementSet or SoftwareLowLevelRequirementSet}. 
        // new relationships:
        sallyModelContents describes SallyTransitionSystemModel with values of type FILE.
        sallyModelNotation describes SallyTransitionSystemModel with a single value of type SallyNotation. 
        sallyModelNotation is a type of wasImpactedBy.
      
      SallyTransitionSystemModelGeneration is a type of ACTIVITY.
        sallyModelGenInfo describes SallyTransitionSystemModelGeneration with values of type GenerationInformation.
        sallyModelGenInfo is a type of used.
        sallyModelGenInfo describes SallyTransitionSystemModelGeneration with at most 1 value.
        // DB 2/25/22: additionally use the 'used' property of ACTIVITY rather than create a new property 'generationInput' 
      
      SallyModelChecking is a type of ANALYSIS.
        // Inherited properties from ANALYSIS
        //    runBy, analyzedWith
        analysisInput of SallyModelChecking only has values of type {SallyTransitionSystemModel or SpecificProperty}.        
        analysisConfiguration of SallyModelChecking only has values of type ToolConfigurationInstance.
      
      SallyNotation (note "Sally notation for creating models and property queries") is a type of ENTITY.
        userGuide describes SallyNotation with values of type DOCUMENT.
        semantics describes SallyNotation with values of type DOCUMENT.
      
      SallyPropertyModel (note "the MODEL class that is used to model a SpecificProperty") is a type of MODEL.
        // this entity models a SpecificProperty that is checked by Sally
        sallyPropertyNotation describes SallyPropertyModel with a single value of type SallyNotation. 
        sallyPropertyNotation is a type of wasImpactedBy.
        sallyPropertySpecification (note "the property content/text") describes SallyPropertyModel with a single value of type string.
        // inherited property
        models of SallyPropertyModel only has values of type SpecificProperty. 

       /////////////////////////////////////////////////////////////////////////
       // Test Oracle and Test generation by Text2Test tool from CLEAR requirements
       
       // DB 2/9/22: Added subclass of TEST_DEVELOPMENT for DesCert test generation activities
       DesCertTestGeneration is a type of TEST_DEVELOPMENT.
         // DB 2/25/22: replaced many properties related to tool usage by using new entity GenerationInformation
         testGenInfo describes DesCertTestGeneration with values of type GenerationInformation.
         testGenInfo describes DesCertTestGeneration with at most 1 value.
         testGenInfo is a type of used.
         // DB 2/25/22: all inputs of test generation 
         testGenInput (note "inputs to this test generation activity") describes DesCertTestGeneration with values of type ENTITY.
         testGenInput is a type of used.

       // DB 2/9/22: Inherit from DesCertTestGeneration, a subclass of TEST_DEVELOPMENT
       ClearTestAndOracleGeneration is a type of DesCertTestGeneration.
         // Test oracles and test generation by Text2Test tool from CLEAR requirements
         // Inherited properties
         //   wasInformedBy: activity that was a precursor to this
         // DB 2/25/22: inherit testGenInfo and testGenInput from DesCertTestGeneration
         testGenInput of ClearTestAndOracleGeneration only has values of type {SoftwareHighLevelRequirementSet or SoftwareLowLevelRequirementSet or DataDictionary}.

       ClearTestingTheory is a type of ENTITY. 
          testingTheoryDoc describes ClearTestingTheory with values of type DOCUMENT. 
       
       TestOracle (note "a test obligation on a specific behavior specified in a sub-clause of a requirement") is a type of ENTITY.
         // DB 2/9/22: verifiesRequirement is a new property in TestOracle, not inherited from ENTITY
         verifiesRequirement (note "the requirement tested by this TestOracle") describes TestOracle with values of type REQUIREMENT. 
         verifiesRequirement describes TestOracle with exactly 1 value. 
         verifiesRequirement is a type of wasImpactedBy.    
         requirementSubclause (note "part (subclause) of the requirement this test oracle tests") describes TestOracle with values of type string.
         testOracleCriteria (note "e.g., equiv class, boundary value") describes TestOracle with a single value of type string.
         testOracleTheory describes TestOracle with a single value of type ClearTestingTheory.
         // Inherited properties:
         //   title: a name for the TestOracle that is more descriptive than the identifier
         
       // DB 2/9/22: TEST class in core ontology is subclassed with further properties
       ClearTest is  a type of TEST.
         // Inherited properties
         //    title - name of the test that can be looked up in the testVectorFile
         //    definedIn denotes test vector File containing tests in format of test vectors
         verifies of ClearTest only has values of type TestOracle.

       // DB 2/9/22: TEST_EXECUTION class in core ontology is extended with further attributes
       DesCertTestExecution is a type of TEST_EXECUTION.
       testEnvironment (note "test environment description, configuration parameters") describes DesCertTestExecution with values of type ENTITY.
       testEnvironment is a type of used.
       
       ///////////////////////////////////////////////////////////////////////
       // Requirement analysis by Text2Test tool from CLEAR requirements
       
       ClearGenericProperty is a type of PROPERTY.
          scopeOf of ClearGenericProperty only has values of type {SoftwareHighLevelRequirementSet or SoftwareLowLevelRequirementSet}. 

       ClearRequirementAnalysis is a type of ANALYSIS.
        // Inherited properties from ANALYSIS
        //   runBy - refers to user
        //   analyzedWith - refers to Text2Test tool
        analysisConfiguration of ClearRequirementAnalysis only has values of type ToolConfigurationInstance.
        analysisInput of ClearRequirementAnalysis only has values of type {DataDictionary or ClearGenericProperty or SoftwareHighLevelRequirementSet or SoftwareLowLevelRequirementSet}.
      
       ////////////////////////////////
       // Test generation by Randoop tool
       
       RandoopJUnitTestGeneration is a type of DesCertTestGeneration.
        // Inherited properties
        //   wasAssociatedWith: refers to user
        //   wasInformedBy: activity that was a precursor to this
        // DB 2/25/22: inherit testGenInfo and testGenInput from DesCertTestGeneration
        testGenInput of RandoopJUnitTestGeneration only has values of type SourceCode.
       
       RandoopTestsAndMetrics
          (note "the set of JUNIT tests/results generated and executed by Randoop with corresponding test classification metrics")
       is a type of TEST.
        // inherited properties:
        //   identifier
        //   generatedAtTime
        // new properties:
        totalNumberOfTestCases (note "size of test set") describes RandoopTestsAndMetrics with a single value of type int.
        numberOfRegressionTestCases (note "tests that pass on the code and should continue to pass on regressions") describes RandoopTestsAndMetrics with a single value of type int.
        numberOfViolationInducingTestCases (note "tests that indicate a method contract violation") describes RandoopTestsAndMetrics with a single value of type int.
        numberOfReducedViolationInducingTestCases (note "numberOfViolationInducingTestCases reduced by filtering out redundant test cases in the same equivalance class") describes RandoopTestsAndMetrics with a single value of type int.
        numberOfErrorRevealingTestCases (note "tests that were determined to reveal actual errors in code") describes RandoopTestsAndMetrics with a single value of type int.
        verifies of RandoopTestsAndMetrics only has values of type SourceCode.
        
        /////////////////////////////////////////////
        // Invariant detection by Daikon
        
        // DB: inherit from ANALYSIS rather than ACTIVITY
        DaikonInvariantDetection is a type of ANALYSIS.
          // Inherited porperties
          //   wasInformedBy: activity that was a precursor to this
          analysisConfiguration of DaikonInvariantDetection only has values of type ToolConfigurationInstance. // DB 2/8/22: not using ConfigurationString 
 
          analyzedWith of DaikonInvariantDetection only has values of type TOOL. 
          analyzedWith describes DaikonInvariantDetection with exactly 1 value. 
          analysisInput of DaikonInvariantDetection only has values of type {SourceCode or RandoopTestsAndMetrics}. 
          
        DaikonInvariantOutput is a type of ENTITY.
          // inherited properties from ENTITY:
          //   identifier
          //   generatedAtTime
          //   wasImpactedBy of DaikonInvariantOutput has values of type RandoopTestsAndMetrics.
          // new properties:
          sourceCode (note "the source code that these invariants hold on") describes DaikonInvariantOutput with values of type SourceCode.
          sourceCode is a type of wasImpactedBy.
          wasGeneratedBy of DaikonInvariantOutput only has values of type DaikonInvariantDetection.
          invariantCount describes DaikonInvariantOutput with a single value of type int.
          classesCount describes DaikonInvariantOutput with a single value of type int.
          testsCount describes DaikonInvariantOutput with a single value of type int.
          supportFiles describes DaikonInvariantOutput with values of type FILE.
          testDriver describes DaikonInvariantOutput with values of type FILE.
          likelyInvariants describes DaikonInvariantOutput with values of type SpecificProperty.
        
        LikelyInvariantModel (note "Specification (model) of invariant detected by Daikon") is a type of MODEL.
          invariantSpecification (note "the likely invariant text") describes LikelyInvariantModel with a single value of type string.
          // inherited property
          models of LikelyInvariantModel only has values of type SpecificProperty.

   