/* Copyright 2023 by Lockheed Martin. */

uri "http://arcos.certgate/LM" alias sacm.
import "http://arcos.rack/PROV-S".


/* Abstract Classes. */

SacmElement is a type of ENTITY.

SubPackage is a type of SacmElement.

ArgumentElement is a type of SacmElement.
	expresses describes ArgumentElement with values of type Expression.
ArtifactElement is a type of SacmElement.
TerminologyElement is a type of SacmElement.
	belongsTo describes TerminologyElement with values of type Category.


/* Base Classes. */

Note
	is a type of SacmElement.
		noteContent describes Note with values of type string.
		noteContent of Note has exactly 1 value.
		annotates describes Note with values of type SacmElement.
		annotates of Note has exactly 1 value.

ImplementationConstraint
	is a type of SacmElement.

AssuranceCasePackage
	is a type of SacmElement.
		containsPackage describes AssuranceCasePackage with values of type SubPackage.


/* Argument Classes */

ArgumentPackage
	is a type of SubPackage.
		isAbstract describes ArgumentPackage with values of type boolean.
		isAbstract of ArgumentPackage has at most 1 value.
		containsElement describes ArgumentPackage with values of type ArgumentElement.

Assertion
	is a type of ArgumentElement.
		subjectTo describes Assertion with values of type ImplementationConstraint.
		inferredFrom describes Assertion with values of type Assertion.
		context describes Assertion with values of type ArgumentElement.

Reasoning
	is a type of Assertion.

Claim
	is a type of Assertion.
		assertionDeclaration describes Claim with values of type string.
		assertionDeclaration of Claim has at most 1 value.
		evidence describes Claim with values of type Reference.

Reference
	is a type of ArgumentElement.
		references describes Reference with values of type Artifact.


/* Artifact Classes */

ArtifactPackage
	is a type of SubPackage.
		containsArtifact describes ArtifactPackage with values of type ArtifactElement.

Artifact
	is a type of ArtifactElement.
		artifactVersion describes Artifact with values of type string.
		artifactDate describes Artifact with values of type dateTime.


 /* Terminology Classes */

 TerminologyPackage
 	is a type of SubPackage.
 		containsTerminology describes TerminologyPackage with values of type TerminologyElement.

 Category
 	is a type of TerminologyElement.

 Expression
 	is a type of TerminologyElement.
 		expressionValue describes Expression with values of type string.
 		expressionValue of Expression has exactly 1 value.
 		uses describes Expression with values of type Term.

 Term
 	is a type of TerminologyElement.
 		termValue describes Term with values of type string.
 		termValue of Term has exactly 1 value.
 